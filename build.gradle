// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}

import org.gradle.plugins.signing.Sign

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        def hasKeyId = project.hasProperty('signing.keyId')
        def hasSecretKeyRingFile = project.hasProperty('signing.secretKeyRingFile')
        def hasPassword = project.hasProperty('signing.password')
        if (!hasKeyId || !hasSecretKeyRingFile || !hasPassword) {
            // Use Java 6's console to read from the console (no good for a CI environment)
            Console console = System.console()
            console.printf "\n\nWe have to sign some things in this build.\n\nPlease enter your signing details.\n\n"

            def id = console.readLine("PGP Key Id: ")
            def file = console.readLine("PGP Secret Key Ring File (absolute path): ")
            def password = console.readPassword("PGP Private Key Password: ")

            allprojects { ext."signing.keyId" = id }
            allprojects { ext."signing.secretKeyRingFile" = file }
            allprojects { ext."signing.password" = password }

            console.printf "\nThanks.\n\n"
        }
    }
}